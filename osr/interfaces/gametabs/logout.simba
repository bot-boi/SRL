(*
Logout
=======
The file holds functions and procedures that are in the logout-tab.
*)

(*
type TRSLogoutWS
~~~~~~~~~~~~~~~~
Type that holds the methods for the in-game worldswitcher located on the logout tab
*)
type
  PRSLogout = ^TRSLogout;
  TRSLogoutWS = record
    FTab: PRSLogout;
  end;

(*
type TRSLogout
~~~~~~~~~~~~~~~
The type that holds functions and properties of the Runescape logout-tab.
*)
  ELogoutSubTab = (lsTabNone, lsTabLogout, lsTabWorldSelect);

  TRSLogout = record(TInterfaceBase)
    btnLogout: TBox;
    WorldSwitcher: TRSLogoutWS;
  end;

{==| Table of Contents |=======================
 procedure TRSLogout.__setup();
 function TRSLogout.Open(): Boolean;
 function TRSLogout.IsOpen(): Boolean;
 function TRSLogout.ClickLogout(attempts:Int32=3; tryTime:Int32=20000): Boolean;
}

(*
var Logout
~~~~~~~~~~
Variable that stores functions and properties of the Runescape logout interface.
*)
var 
  Logout: TRSLogout;
  
{$IFNDEF CODEINSIGHT}
procedure TRSLogout.__Setup();
begin
  with Self do
  begin
    FName := 'Logout';
    FStatic := True;
    FBounds := [550,206,733,464];

    btnLogout  := [575,420,708,445];

    WorldSwitcher.FTab := PRSLogout(@Self);
  end;
end;
{$ENDIF}

(*
Logout.Open
~~~~~~~~~~~
.. code-block:: pascal

  function TRSLogout.Open(): Boolean;

Opens the interface
*)
function TRSLogout.Open(): Boolean;
begin
  Result := Gametabs.Open(tabLogout);
end;

(*
Logout.IsOpen
~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSLogout.IsOpen(Tab:ELogoutSubtab=lsTabNone; maxWait:Int32=0): Boolean;

Checks if the interface is already open. Can also be used to check if a specific "subtab" is open.
*)
function TRSLogout.IsOpen(Tab: ELogoutSubtab = lsTabNone; maxWait: Int32 = 0): Boolean;
begin
  Result := Gametabs.IsOpen(tabLogout, maxWait);
  if (Result) and (Tab <> lsTabNone) then
    case Tab of
      lsTabWorldSelect: Result := WorldSwitcher.isOpen();
      lsTabLogout: Result := True;
    end;
end;

(*
Logout.ClickLogout
~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSLogout.ClickLogout(attemts:Int32=3; tryTime:Int32=20000): Boolean;

Clicks the logout button, by default retires 3 times over 20 seconds.
*)
function TRSLogout.ClickLogout(attemts:Int32=3; tryTime:Int32=20000): Boolean;
var 
  t,seed:UInt64;
begin
  if (not Self.Open()) then
    Exit(False);
  if (WorldSwitcher.isOpen()) then
    WorldSwitcher.Close();
  
  seed := 3153;
  Inc(tryTime, attemts * seed div 2);
  for 1 to attemts do
  begin
    mouse.click(Self.BtnLogout, mouse_left);
    t := GetTickCount64() + (tryTime div attemts);
    repeat
      if (CountColorTolerance(1777019, Self.BtnLogout, 30) < 100) then
        Exit(True)
      else
        Wait(25);
    until GetTickCount64() > t;
    Wait(0,seed);
  end;
end;

(*
Logout.SwitchToWorld
~~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSLogout.SwitchToWorld(World: Integer): Boolean;

Jumps to the given world.
*)
function TRSLogout.SwitchToWorld(World: Integer): Boolean;
begin
  Result := Self.WorldSwitcher.SwitchTo(World);
end;

(*
Logout.WorldSwitcher.GetCurrentWorld
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSLogoutWS.GetCurrentWorld: Int32;

Reads and returns the current world.
*)
function TRSLogoutWS.GetCurrentWorld: Int32;
const
  CURRENT_WORLD_COLOR = $1F98FF;
  CURRENT_WORLD_BOUNDS: TBox = [555, 206, 714, 226];
var
  Numbers: TExtendedArray;
begin
  Result := -1;

  if FTab^.Open() then
  begin
    Numbers := OCR.SimpleRecognize(CURRENT_WORLD_BOUNDS, CURRENT_WORLD_COLOR, UpFont).ExtractNumbers();
    if Length(Numbers) > 0 then
      Result := Round(Numbers[0]);
  end;
end;

(*
Logout.WorldSwitcher.isOpen
~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSLogoutWS.isOpen(WaitTime: Integer = 0): Boolean;

Returns if the in-game worldswitcher is open
*)
function TRSLogoutWS.IsOpen: Boolean; overload;
begin
  Result := Self.GetCurrentWorld() > 0;
end;

function TRSLogoutWS.IsOpen(WaitTime: Int32): Boolean; overload;
var
  T: UInt64 := GetTickCount() + WaitTime;
begin
  while (T > GetTickCount()) do
  begin
    if Self.GetCurrentWorld() > 0 then
      Exit(True);

    Wait(50);
  end;
end;

(*
Logout.WorldSwitcher.Open
~~~~~~~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSLogoutWS.Open: Boolean;

Opens the in-game worldswitcher
*)
function TRSLogoutWS.Open: Boolean;
const
  WORLD_SWITCHER_BUTTON: TBox = [573, 372, 708, 396];
begin
  if Self.IsOpen() then
    Exit(True);

  Mouse.Click(WORLD_SWITCHER_BUTTON, MOUSE_LEFT);
  if Self.IsOpen(Random(2500, 3000)) then
    Exit(True);
end;

(*
Logout.WorldSwitcher.Close
~~~~~~~~~~~~~~~~~~~~~~~~~~
.. code-block:: pascal

  function TRSLogoutWS.Close: Boolean;

Closes the worldswitcher (returns back to the logout button)
*)
function TRSLogoutWS.Close: Boolean;
var
  t: UInt64;
begin
  if (not Self.isOpen()) then
    Exit(True);
  Mouse.Click(Box(715, 206, 735, 226), mouse_Left);

  t := GetTickCount64() + Random(4000, 5000);
  while (t > GetTickCount64()) do
    if (not Self.isOpen()) then
      Exit(True)
    else
      Wait(Random(50, 100));
end;

(*
Logout.WorldSwitcher.ClickWorld
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSLogoutWS.ClickWorld(World, WorldColor: Int32): Boolean;

Attempts to click a world [Using a simple bitmap search].
*)
function TRSLogoutWS.ClickWorld(World, WorldColor: Int32): Boolean;
const
  WORLD_WIDTH = 145;
  WORLD_HEIGHT = 12;
  WORLD_FONT = 'SmallChars07';
var
  BMP: TMufasaBitmap;
  B: TBox;
begin
  BMP := GetMufasaBitmap(BitmapFromText(ToString(World), WORLD_FONT));
  BMP.ReplaceColor($FFFFFF, WorldColor);

  Result := FindBitmapIn(BMP.GetIndex(), B.X1, B.Y1, Self.FTab^.GetBounds().Expand(1));
  if Result then
  begin
    B.X2 := B.X1 + WORLD_WIDTH;
    B.Y2 := B.Y1 + WORLD_HEIGHT;

    Mouse.Click(B, MOUSE_LEFT);
  end;

  BMP.Free();
end;

(*
Logout.WorldSwitcher.Scroll
~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSLogoutWS.Scroll(Down: Boolean): Boolean;

    Returns true if world-switcher was scrolled [By checking for pixelshift].
*)
function TRSLogoutWS.Scroll(Down: Boolean): Boolean;
var
  B: TBox;
  Colors: TIntegerArray;
begin
  B := Self.FTab^.GetBounds();
  B := B.Expand(-15, -35); // Scrollable area

  Colors := GetColors(TPAFromBox(B));

  Mouse.Scroll(B, Random(1, 2), Down);
  if not Colors.Equals(GetColors(TPAFromBox(B))) then
    Exit(True);
end;

(*
Logout.WorldSwitcher.SwitchTo
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSLogoutWS.SwitchTo(World: Int32): Boolean;

Uses the in-game world switcher to switch to `World`. Returns true if succesfully
switched [By reading current world].

**Example**

.. pascal::
  if Logout.WorldSwitcher.SwitchTo(302) then
    WriteLn('Switched to world 302');
*)
function TRSLogoutWS.SwitchTo(World: Int32): Boolean;
const
  COLOR_MEMBERS_WORLD = $00F0F0;
  COLOR_FREE_WORLD = $E0E0E0;

  function WorldSwitch(Direction: Boolean): Boolean;
  begin
    repeat
      if Self.ClickWorld(World, COLOR_MEMBERS_WORLD) or
         Self.ClickWorld(World, COLOR_FREE_WORLD) then
      begin
        Chatbox.ClickOption(['Yes.'], Random(1500, 2000));
        if SRL.IsClientLoading(Random(1500, 2000)) then
          SRL.WaitClientLoading();

        Exit(True);
      end;

      Wait(0, 800, wdLeft);
    until not Self.Scroll(Direction);
  end;

var
  Direction: Boolean;
begin
  if (not Self.Open()) then
    Exit(False);

  Direction := SRL.Dice(50);
  if WorldSwitch(Direction) or WorldSwitch(not Direction) then
    Result := Self.GetCurrentWorld() = World;
end;

begin
  Logout.__Setup();
end;
